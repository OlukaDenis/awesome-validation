plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def keystorePropertiesFile = rootProject.file("secrets.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.basgeekball.awesomevalidation'
    compileSdk 34
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        minSdk 21
        targetSdk 34
        defaultPublishConfig "release"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    sourceSets {
        main {
            java.srcDirs = ["src/main/java"]
            resources.srcDirs = ["src/main/res"]
        }
        test {
            java.srcDirs = ["src/test/java"]
            resources.srcDirs = ["src/test/res"]
        }
        androidTest.setRoot("test")
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'

    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation("com.google.guava:guava:30.1.1-android") {
        exclude module: "jsr305"
    }
    // Streams
//    implementation 'net.sourceforge.streamsupport:streamsupport:1.7.4'

    testImplementation "junit:junit:4.13.1"
    testImplementation "org.mockito:mockito-core:3.7.0"
    testImplementation "org.easymock:easymock:4.2"
    testImplementation "org.powermock:powermock-module-junit4:2.0.9"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.9"
    testImplementation "org.powermock:powermock-api-easymock:2.0.9"
}

configurations.all {
    resolutionStrategy {
        force "junit:junit:4.13.1"
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.sourceFiles
}

afterEvaluate {
    publishing {
        publications {
            // Creates a publication named - release
            release(MavenPublication) {
                artifact(bundleReleaseAar)
                artifact(androidSourcesJar)
                groupId 'com.interswitchug'
                artifactId 'awesome-validation'
                version '0.0.3'
            }
        }

        repositories {
            maven {
                name 'MobileDevOps'
                url keystoreProperties['repositoryUrl']
                credentials {
                    username keystoreProperties['userName']
                    password keystoreProperties['azureMavenAccessToken']
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}
